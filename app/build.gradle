plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.mikepenz.aboutlibraries.plugin'
}

android {
    compileSdk 34
    namespace 'de.nulide.findmydevice'
    packagingOptions {
        resources {
            excludes += ['META-INF/LICENSE.md', 'META-INF/NOTICE.md']
        }
    }

    lint {
        baseline = file("lint-baseline.xml")
        lintOptions {
            // There may be extra translations (strings in other languages that don't exist
            // in strings.xml anymore) temporarily, until Weblate automatically deletes them.
            disable 'ExtraTranslation'
            disable 'MissingTranslation'
        }
    }

    defaultConfig {
        applicationId "de.nulide.findmydevice"
        minSdkVersion 24
        targetSdkVersion 34
        versionCode 29
        versionName "0.7.1"
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "default"
    productFlavors {
        prod {
            dimension "default"
            buildConfigField "String", "DEFAULT_FMD_COMMAND", "\"fmd\""
        }
        dev {
            dimension "default"
            applicationIdSuffix ".dev"
            buildConfigField "String", "DEFAULT_FMD_COMMAND", "\"fmdev\""
        }
    }

    // https://developer.android.com/guide/topics/resources/app-languages
    androidResources {
        generateLocaleConfig true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlin {
        jvmToolchain(17)
    }

    buildFeatures {
        buildConfig true
        viewBinding true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // AndroidX
    implementation 'androidx.activity:activity-ktx:1.9.2'
    implementation 'androidx.annotation:annotation:1.8.2'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.concurrent:concurrent-futures-ktx:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "androidx.fragment:fragment-ktx:1.8.4"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.8.6'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.6'
    implementation 'androidx.preference:preference-ktx:1.2.1'

    // CameraX
    def camerax_version = "1.3.4"
    implementation "androidx.camera:camera-core:${camerax_version}"
    implementation "androidx.camera:camera-camera2:${camerax_version}"
    implementation "androidx.camera:camera-lifecycle:${camerax_version}"
    implementation "androidx.camera:camera-video:${camerax_version}"
    implementation "androidx.camera:camera-view:${camerax_version}"
    implementation "androidx.camera:camera-extensions:${camerax_version}"

    // Testing
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'

    implementation 'com.android.volley:volley:1.2.1'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'com.google.code.gson:gson:2.11.0'

    implementation 'com.github.iamrobj:NotificationHelperLibrary:2.0.5'
    implementation 'com.github.UnifiedPush:android-connector:2.1.1'
    implementation "com.mikepenz:aboutlibraries:11.2.3"
    implementation "org.apache.maven:maven-artifact:3.9.9" // for semantic versions
    implementation 'org.bouncycastle:bcprov-jdk18on:1.78.1'


    // XXX: only used by javax.xml.bind.DatatypeConverter
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:2.3.2'
    implementation 'xerces:xercesImpl:2.12.1'

    // Shizuku
    implementation "dev.rikka.shizuku:api:12.1.0"
    implementation "dev.rikka.shizuku:provider:12.1.0"

    // https://stackoverflow.com/questions/75263047/duplicate-class-in-kotlin-android
    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }
}
